// This tests shutdown through coordination (request)
target C

reactor E1 {
  state cnt: int = 0

  timer t(0, 100 msec)
  output out: instant_t

  reaction(t) -> out {=
    lf_print("E1 timer trigger");
    self->cnt++;
    lf_set(out, lf_time_logical_elapsed());

    if (self->cnt == 5) {
        lf_print("E1 request stop");
        lf_request_stop();
    }
  =}

  reaction(shutdown) {=
    lf_print("E1 shut down");
    lf_assert(self->cnt == 5, "Enclave shutdown prematurely");
  =}
}

reactor E2 {
  state cnt: int = 0

  input in: instant_t

  timer t(50 msec, 100 msec)

  reaction(in) {=
    lf_print("E2 triggered by in");
  =}

  reaction(t) {=
    lf_print("E2 timer triggered");
    self->cnt++;
  =}

  reaction(shutdown) {=
    lf_print("E2 shut down");
    lf_assert(self->cnt == 4, "Enclave shutdown prematurely");
  =}
}

main reactor {
  @enclave
  e1 = new E1()

  @enclave
  e2 = new E2()
}
