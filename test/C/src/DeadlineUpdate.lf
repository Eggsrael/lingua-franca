// Test of lf_update_deadline() API for updating deadlines dynamically.
// This test covers two scenarios:
// 1. Updating to an earlier deadline: Verify that a 100 ms declared deadline
// can be shortened to 50 ms at runtime, and that a violation occurs
// accordingly.
// 2. Updating to a later deadline: Verify that a 50 ms declared deadline
// can be relaxed to 100 ms at runtime, and violation is triggered at
// the updated time 100 ms.
// The test prints SUCCESS if both cases behave as expected, otherwise it
// reports an error and exits.
target C

reactor A {
  state i: int
  logical action a

  // Test Case 1: Verify that decreasing the deadline works.
  // The reaction starts with a declared deadline of 100 ms, but we update it
  // to 50 ms at runtime. A deadline violation should therefore be detected
  // around physical time 50 ms . If no violation is detected
  // after 50 ms, the test fails.
  reaction(startup) -> a {=
    self->i = 0;
    lf_update_deadline(self, MSEC(50));
    while(!lf_check_deadline(self, true)){
      if(self->i > 1) {
        lf_print_error_and_exit("Expected 1, but got %d", self->i);
      }
      self->i++;
      lf_print("Deadline violation not detected on Logical time: %lld msecs. Physical time: %lld msecs.", lf_time_logical_elapsed() / MSEC(1), lf_time_physical_elapsed() / MSEC(1));
      lf_sleep(MSEC(50));
    }
  =} deadline(100 msec) {=
    // With the updated deadline, this handler should fire around 20 ms.
    lf_print("Deadline violation detected on Logical time: %lld msecs. Physical time: %lld msecs.", lf_time_logical_elapsed() / MSEC(1), lf_time_physical_elapsed() / MSEC(1));
    lf_schedule(a, MSEC(100)); // Reset lag.
  =}

  // Test Case 2: Verify that increasing the deadline works.
  // The reaction is declared with a 50 ms deadline, but we update it
  // to 100 ms at runtime. A violation should NOT be detected at 50 ms,
  // because the updated deadline is 100 ms. Entering the deadline
  // handler below would indicate failure.
  reaction(a) {=
    self->i = 0;
    lf_update_deadline(self, MSEC(100));
    while(!lf_check_deadline(self, true)){
      self->i++;
      lf_print("Deadline violation not detected on Logical time: %lld msecs. Physical time: %lld msecs.", lf_time_logical_elapsed() / MSEC(1), lf_time_physical_elapsed() / MSEC(1));
      lf_sleep(MSEC(50));
    }
  =} deadline(50 msec) {=
    lf_print("Deadline violation detected on Logical time: %lld msecs. Physical time: %lld msecs.", lf_time_logical_elapsed() / MSEC(1), lf_time_physical_elapsed() / MSEC(1));
    if (self->i >= 2) {
      lf_print("SUCCESS");
    } else {
      lf_print_error_and_exit("Expected greater than or equal to 2, but got %d", self->i);
    }
  =}
}

main reactor {
  a = new A()
}
